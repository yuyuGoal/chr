module;

import lib.ft4.core.accounts.{ account, single_sig_auth_descriptor, create_account_with_auth };
import lib.ft4.auth;


entity user {
    mutable name;
    key id: byte_array;
    key account;
}

entity follower {
    index user;
    index follower: user;
    key user, follower;
}

entity post {
    timestamp = op_context.last_block_time;
    index user;
    content: text;
}

function format_user(user) = "%s#%s".format(user.name, user.id.to_hex().sub(0, 5));
function require_is_signer(pubkey) = require(op_context.is_signer(pubkey), "User must sign this operation");
function require_user(id: byte_array) = require(user @? { id }, "User with id %b does not exist".format(id));
function limit_content(content: text) = require(content.size() <= 255, "A post cannot be longer than 255 letters");

operation create_user(name, pubkey) {
    val account = create_account_with_auth(single_sig_auth_descriptor(pubkey, set(["A", "T", "MySession"])));
    create user ( name, pubkey, account );
}

operation make_post(content: text) {
    val account = auth.authenticate();
    limit_content(content);
    val user = user @ { account };
    create post ( user, content );
}

operation follow_user(follow_id: byte_array) {
    val account = auth.authenticate();
    val user = user @ { account };
    val follow = require_user(follow_id);
    create follower(user = follow, follower = user);
}

operation unfollow_user(unfollow_id: byte_array) {
    val account = auth.authenticate();
    val user = user @ { account };
    val unfollow = require_user(unfollow_id);
    delete follower @? { .user == unfollow, .follower == user };
}

query get_user_name(user_id: byte_array): text {
    return format_user(require_user(user_id));
}

query get_user_id(name: text): byte_array {
    return user @ { .name == name } (.id);
}

query get_followers_count(user_id: byte_array): integer {
    return follower @ { .user == require_user(user_id) } (@sum 1);
}

query get_following_count(user_id: byte_array): integer {
    return follower @ { .follower == require_user(user_id) } (@sum 1);
}

query is_following(my_id: byte_array, your_id: byte_array) =
    exists(follower @? { .user.id == your_id, .follower.id == my_id });

struct post_dto {
    timestamp;
    user: struct<user>;
    content: text;
}

query get_posts(
    user_id: byte_array,
    pointer: integer,
    n_posts: integer
): (pointer: integer, posts: list<post_dto>) {
    val posts = (user, follower, post) @* {
        user.id == user_id,
        follower.follower == user,
        post.user == follower.user
    } (
        @sort_desc @omit post.rowid,
        post_dto(
            post.timestamp,
            post.content,
            user = post.user.to_struct()
        )
    ) offset pointer limit n_posts;

    return (
        pointer = pointer + posts.size(),
        posts = posts
    );
}

query get_users(pointer: integer, n_users: integer) {
    val users = user @* {} (name = format_user($), id = .id) offset pointer limit n_users;
    return (
        pointer = pointer + users.size(),
        users = users
    );
}

@extend(auth.auth_handler)
function () = auth.add_auth_handler(
    flags = ["MySession"]
);